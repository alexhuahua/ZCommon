apply plugin: 'com.android.library'
//apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.yuyuyu123'
android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    compileOptions {
        // Gradle JDK Version
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
//    testCompile 'junit:junit:4.12'
    // http://developer.android.com/intl/zh-cn/tools/support-library/index.html
    api "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    api "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    api "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    api "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    api "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    api "org.greenrobot:eventbus:$EVENT_BUS_VERSION"
    // https://github.com/ReactiveX/RxAndroid
    api "io.reactivex.rxjava2:rxjava:$RXJAVA_VERSION"
    api "io.reactivex.rxjava2:rxandroid:$RXANDROID_VERSION"
    // https://github.com/square/retrofit
    api "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    api "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    api "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    api "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    api "com.squareup.retrofit2:retrofit-converters:$RETROFIT_VERSION"
    api "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    api "com.squareup.retrofit2:retrofit-adapters:$RETROFIT_VERSION"
    // https://github.com/afollestad/material-dialogs
    api("com.afollestad.material-dialogs:commons:$COMMONS_DIALOG_VERSION") {
        exclude group: 'com.android.support' //by group
    }
    api "com.readystatesoftware.systembartint:systembartint:$SYSTEM_BAR_TINT_VERSION"
    api "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"
//    api "com.github.bumptech.glide:okhttp3-integration:$GLIDE_VERSION"
    api "org.aspectj:aspectjrt:$ASPECTJ_VERSION"
}

publish {
    repoName="ZCommon"
    userOrg = 'liuliwei49'
    groupId = 'com.cc.android'
    artifactId = 'ZCommon'
    publishVersion = '1.0.0'
    desc = 'A library for fast developing android applications.'
    website = 'https://github.com/yuyuyu123/ZCommon'
}

tasks.withType(Javadoc) {//防止编码问题
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

//task makeJar(type: org.gradle.api.tasks.Copy){
//    delete 'build/libs/ZCommon.jar'
//    from('build/intermediates/bundles/release/')
//    into('build/libs/')
//    include('classes.jar')
//    rename('classes.jar','ZCommon.jar')
//}
//makeJar.dependsOn(build)


import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

android.libraryVariants.all { variant ->
    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.8",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", android.bootClasspath.join(
                File.pathSeparator)]

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)

        def log = project.logger
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break
                case IMessage.WARNING:
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break
            }
        }
    }
}
